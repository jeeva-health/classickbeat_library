plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def apiKeyPropertiesFile = rootProject.file("apikeys.properties")
def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "ai.heart.classickbeats"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 2
        versionName "0.02-beta"

        renderscriptTargetApi 24
        renderscriptSupportModeEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        def BASE_URL = "BASE_URL"
        def CLIENT_ID = "CLIENT_ID"
        def CLIENT_SECRET = "CLIENT_SECRET"

        debug {
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'

            buildConfigField "String", BASE_URL, apiKeyProperties['DEV_BASE_URL']
            buildConfigField "String", CLIENT_ID, apiKeyProperties['DEV_CLIENT_ID']
            buildConfigField "String", CLIENT_SECRET, apiKeyProperties['DEV_CLIENT_SECRET']
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", BASE_URL, apiKeyProperties['PROD_BASE_URL']
            buildConfigField "String", CLIENT_ID, apiKeyProperties['PROD_CLIENT_ID']
            buildConfigField "String", CLIENT_SECRET, apiKeyProperties['PROD_CLIENT_SECRET']
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE.md', 'META-INF/LICENSE-notice.md']
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_stdlib"
    implementation "androidx.core:core-ktx:$core_ktx"
    implementation "androidx.activity:activity-ktx:$activity_ktx"
    implementation "androidx.fragment:fragment-ktx:$fragment_ktx"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation project(path: ':model')
    implementation project(path: ':shared')

    implementation "androidx.recyclerview:recyclerview:1.2.1"

    // lifecycle -- (used for ViewBinding delegate)
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.1"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-fragment:1.0.0'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"


    // Circle ImageView
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // CameraX
    implementation "androidx.camera:camera-camera2:1.1.0-beta02"
    implementation "androidx.camera:camera-lifecycle:1.1.0-beta02"
    implementation 'androidx.camera:camera-view:1.1.0-beta02'

    //Lottie
    implementation 'com.airbnb.android:lottie:5.0.3'


    //Glide
    implementation 'com.github.bumptech.glide:glide:4.13.1'
    kapt 'com.github.bumptech.glide:compiler:4.13.1'

    // Graph
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    implementation "org.apache.commons:commons-math3:3.6.1"
    implementation "uk.me.berndporr:iirj:1.3"
    implementation('com.github.psambit9791:jdsp:0.6.0')
            {
                exclude group: 'org.apache.maven.surefire', module: 'common-java'
                exclude group: 'org.apache.maven.surefire', module: 'surefire-api'
            }

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:27.1.0')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.android.gms:play-services-auth:20.1.0'
    implementation 'com.google.firebase:firebase-messaging:23.0.0'

    implementation 'com.google.android.play:core-ktx:1.8.1'

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:$version_moshi"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$version_moshi"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$version_retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$version_retrofit"
    implementation "com.squareup.okhttp3:okhttp:$version_okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$version_okhttp"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$version_retrofit_coroutines_adapter"
    implementation 'org.conscrypt:conscrypt-android:2.5.2'

    // Room
    implementation "androidx.room:room-runtime:2.4.2"
    kapt "androidx.room:room-compiler:2.4.2"
    implementation "androidx.room:room-ktx:2.4.2"
    implementation "androidx.room:room-paging:2.4.2"

    // Pagination
    implementation "androidx.paging:paging-runtime-ktx:3.1.0"

    // ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer-core:2.17.0'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.17.0'

    // Leak Canary
    // debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    // TEST Dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.paging:paging-common-ktx:3.1.0"

    // Core library
    testImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:core:1.4.0'

    // Mockito framework
    testImplementation 'org.mockito:mockito-core:4.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.4.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.4.0'

    //support design
    implementation 'com.google.android.material:material:1.6.0-alpha03'

    // The following Espresso dependency can be either "implementation"
    // or "androidTestImplementation", depending on whether you want the
    // dependency to appear on your APK's compile classpath or the test APK
    // classpath.
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
}