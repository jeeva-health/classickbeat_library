plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def apiKeyPropertiesFile = rootProject.file("apikeys.properties")
def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {

        def BASE_URL = "BASE_URL"
        def CLIENT_ID = "CLIENT_ID"
        def CLIENT_SECRET = "CLIENT_SECRET"

        debug {
            debuggable true

            buildConfigField "String", BASE_URL, apiKeyProperties['DEV_BASE_URL']
            buildConfigField "String", CLIENT_ID, apiKeyProperties['DEV_CLIENT_ID']
            buildConfigField "String", CLIENT_SECRET, apiKeyProperties['DEV_CLIENT_SECRET']
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", BASE_URL, apiKeyProperties['PROD_BASE_URL']
            buildConfigField "String", CLIENT_ID, apiKeyProperties['PROD_CLIENT_ID']
            buildConfigField "String", CLIENT_SECRET, apiKeyProperties['PROD_CLIENT_SECRET']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.10"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation project(path: ':model')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    api 'com.jakewharton.timber:timber:4.7.1'

    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_kotlin_coroutines"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3"

    // Hilt
    implementation "com.google.dagger:hilt-android:2.35.1"
    implementation 'androidx.hilt:hilt-navigation-fragment:1.0.0'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Moshi
    implementation "com.squareup.moshi:moshi:1.12.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.12.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$version_moshi"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$version_retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$version_retrofit"
    implementation "com.squareup.okhttp3:okhttp:$version_okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$version_okhttp"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$version_retrofit_coroutines_adapter"
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
}